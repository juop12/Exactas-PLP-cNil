% Ejercicio 1

% matriz(+F, +C, -M) es verdadero si M es una matriz de F filas y C columnas. 
% Cuando M no está instanciada el predicado debe generar una matriz con variables no instanciadas en las celdas
matriz(0,_,[]).
matriz(F,C,[F1|Fs]) :- 
	F > 0, 					% F es una cantidad positiva de filas
	C >= 0, 				% C es una cantidad no negativa de columnas
	Fp is F - 1, 			
	length(F1, C), 			% La primer fila tiene C columnas
	matriz(Fp, C, Fs).		% La matriz Fs es de dimension Fp x C

% Ejercicio 2

% replicar(+Elem, +N, -Lista) es cierto cuando Lista es una lista de longitud N, 
% donde cada elemento es igual a Elem.

replicar(Elem,N,L) :- 
	length(L, N),		% L es de longitud N
	maplist(=(Elem),L).	% Todos los elementos de L son Elem


% Ejercicio 3

% TODO: Documentar 
appendColumnas([],[],[]). 
appendColumnas([F1|TH],[T|TC],[[F1|T]|TS]) :- appendColumnas(TH, TC, TS). 

% transponer(+M, -MT) es cierto cuando MT es la matriz transpuesta de M. La transpuesta,
% MT, tiene como filas las columnas de M y viceversa. Asumir que M es una matriz bien formada (todas las filas tienen
% la misma longitud)
transponer([],[]).
transponer([[]|_], []).
transponer(M, [Mt1|Mts]) :- appendColumnas(Mt1, Ms, M), transponer(Ms, Mts).


% Predicado dado armarNono/3
armarNono(RF, RC, nono(M, RS)) :-
	length(RF, F),
	length(RC, C),
	matriz(F, C, M),
	transponer(M, Mt),
	zipR(RF, M, RSFilas),
	zipR(RC, Mt, RSColumnas),
	append(RSFilas, RSColumnas, RS).

zipR([], [], []).
zipR([R|RT], [L|LT], [r(R,L)|T]) :- zipR(RT, LT, T).

% Ejercicio 4

pintadasValidas(r([],L)) :- replicar(o,_,L).  % Cuando no hay restricciones, L es toda blanca

pintadasValidas(r([R|[]], L)) :- 		
	append(Prefijo,Sufijo, L),  		 % L es la concatenacion de Prefijo con Sufijo
	replicar(o,_,Prefijo), 				 % Todos los elementos de Prefijo son blancos
	replicar(x,R,BloqueX), 				 % BloqueX es lista de longitud R que tiene todas las celdas pintadas de negro
	append(BloqueX, Resto, Sufijo), 	 % Sufijo es la concatenacion de BloqueX con Resto
	pintadasValidas(r([],Resto)).		 % Resto esta bien pintada (como no hay restricciones, tiene que ser todo blanco)
 
pintadasValidas(r([R|RS], L)) :- 		 
	RS \= [], 							 % Hay mas de 1 restriccion
	append(Prefijo,Sufijo, L), 			 % L es concatenacion de Prefijo con Sufijo
	replicar(o,_,Prefijo), 				 % Todos los elementos de Prefijo son blancos
	replicar(x,R,BloqueX), 				 % BloqueX es lista de longitud R con todas las celdas pintadas de negro
	append(BloqueX, [o|Resto], Sufijo),	 % Sufijo es  BloqueX ++ [o] ++ Resto
	pintadasValidas(r(RS,Resto)).		 % Resto esta bien pintado sujeto a las demás restricciones  

% Ejercicio 5
% Asumo que ya es un nonograma valido -> Matriz y restricciones bien formadas.

resolverNaive(nono(_,Restricciones)) :- maplist(pintadasValidas, Restricciones). 

% Solucion mas a manopla
% resolverFilasNaive([],[]). 

% resolverFilasNaive([F|Fs], [r(R,F)|Rs]) :- 
% 	pintadasValidas(r(R,F)), 				% La Fila esta bien pintada
% 	resolverFilasNaive(Fs, Rs). 			% El resto de las filas estan bien pintadas

% resolverNaive(nono([F|Fs],Restricciones)) :- 
% 	length([F|Fs], _nroFilas), 											% Conseguir cantidad de filas
% 	length(F, _nroColumnas), 											% Conseguir cantidad de columnas
% 	length(RestriccionesFilas, _nroFilas), 								% Asegurar de tener la cantidad correcta de restricciones para las filas
% 	length(RestriccionesColumnas, _nroColumnas), 						% Asegurar de tener la cantidad correcta de restricciones para las columnas
% 	append(RestriccionesFilas, RestriccionesColumnas, Restricciones), 	% Restricciones = RestriccionesFilas ++ RestriccionesColumnas
% 	resolverFilasNaive([F|Fs],RestriccionesFilas), 						% Esta bien pintado segun las restricciones de las filas
% 	transponer([F|Fs], NNt),											% Conseguir el tablero traspuesto
% 	resolverFilasNaive(NNt,RestriccionesColumnas).						% El tablero traspuesto esta bien pintado segun las restricciones de las columnas

% Ejercicio 6
interseccion([L|[]],L).
interseccion([P1,P2|P], L) :- 
	maplist(combinarCelda, P1, P2, Lp), % Lp es la interseccion entre P1 y P2
	interseccion([Lp|P], L).			% L es la interseccion entre Lp y el resto de las pintadas

pintarObligatorias(r(R,L)) :-
	findall(L, pintadasValidas(r(R,L)), ListaDePintadasValidas), % Conseguimos todas las formas de pintar validas.
	interseccion(ListaDePintadasValidas, L).					 % Veamos que L sea la interseccion entre todas las formas de pintar validas. 

% Predicado dado combinarCelda/3
combinarCelda(A, B, _) :- var(A), var(B).
combinarCelda(A, B, _) :- nonvar(A), var(B).
combinarCelda(A, B, _) :- var(A), nonvar(B).
combinarCelda(A, B, A) :- nonvar(A), nonvar(B), A = B.
combinarCelda(A, B, _) :- nonvar(A), nonvar(B), A \== B.

% Ejercicio 7
% TODO: REVISAR (salió de taquito, por eso.)
deducir1Pasada(nono(_,R)) :- maplist(pintarObligatorias,R).

% Predicado dado
cantidadVariablesLibres(T, N) :- term_variables(T, LV), length(LV, N).

% Predicado dado
deducirVariasPasadas(NN) :-
	NN = nono(M,_),
	cantidadVariablesLibres(M, VI), % VI = cantidad de celdas sin instanciar en M en este punto
	deducir1Pasada(NN),
	cantidadVariablesLibres(M, VF), % VF = cantidad de celdas sin instanciar en M en este punto
	deducirVariasPasadasCont(NN, VI, VF).

% Predicado dado
deducirVariasPasadasCont(_, A, A). % Si VI = VF entonces no hubo más cambios y frenamos.
deducirVariasPasadasCont(NN, A, B) :- A =\= B, deducirVariasPasadas(NN).

% Ejercicio 8
restriccionConMenosLibres(_, _) :- completar("Ejercicio 8").

% Ejercicio 9
resolverDeduciendo(NN) :- completar("Ejercicio 9").

% Ejercicio 10
solucionUnica(NN) :- completar("Ejercicio 10").

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                              %
%    Ejemplos de nonogramas    %
%        NO MODIFICAR          %
%    pero se pueden agregar    %
%                              %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Fáciles
nn(0,  NN) :- armarNono([[1],[2]],[[],[2],[1]], NN).
nn(1,  NN) :- armarNono([[4],[2,1],[2,1],[1,1],[1]],[[4],[3],[1],[2],[3]], NN).
nn(2,  NN) :- armarNono([[4],[3,1],[1,1],[1],[1,1]],[[4],[2],[2],[1],[3,1]], NN).
nn(3,  NN) :- armarNono([[2,1],[4],[3,1],[3],[3,3],[2,1],[2,1],[4],[4,4],[4,2]], [[1,2,1],[1,1,2,2],[2,3],[1,3,3],[1,1,1,1],[2,1,1],[1,1,2],[2,1,1,2],[1,1,1],[1]], NN).
nn(4,  NN) :- armarNono([[1, 1], [5], [5], [3], [1]], [[2], [4], [4], [4], [2]], NN).
nn(5,  NN) :- armarNono([[], [1, 1], [], [1, 1], [3]], [[1], [1, 1], [1], [1, 1], [1]], NN).
nn(6,  NN) :- armarNono([[5], [1], [1], [1], [5]], [[1, 1], [2, 2], [1, 1, 1], [1, 1], [1, 1]], NN).
nn(7,  NN) :- armarNono([[1, 1], [4], [1, 3, 1], [5, 1], [3, 2], [4, 2], [5, 1], [6, 1], [2, 3, 2], [2, 6]], [[2, 1], [1, 2, 3], [9], [7, 1], [4, 5], [5], [4], [2, 1], [1, 2, 2], [4]], NN).
nn(8,  NN) :- armarNono([[5], [1, 1], [1, 1, 1], [5], [7], [8, 1], [1, 8], [1, 7], [2, 5], [7]], [[4], [2, 2, 2], [1, 4, 1], [1, 5, 1], [1, 8], [1, 7], [1, 7], [2, 6], [3], [3]], NN).
nn(9,  NN) :- armarNono([[4], [1, 3], [2, 2], [1, 1, 1], [3]], [[3], [1, 1, 1], [2, 2], [3, 1], [4]], NN). % Tiene varias soluciones
nn(10, NN) :- armarNono([[1], [1], [1], [1, 1], [1, 1]], [[1, 1], [1, 1], [1], [1], [ 1]], NN).
nn(11, NN) :- armarNono([[1, 1, 1, 1], [3, 3], [1, 1], [1, 1, 1, 1], [8], [6], [10], [6], [2, 4, 2], [1, 1]], [[2, 1, 2], [4, 1, 1], [2, 4], [6], [5], [5], [6], [2, 4], [4, 1, 1], [2, 1, 2]], NN).
nn(12, NN) :- armarNono([[9], [1, 1, 1, 1], [10], [2, 1, 1], [1, 1, 1, 1], [1, 10], [1, 1, 1], [1, 1, 1], [1, 1, 1, 1, 1], [1, 9], [1, 2, 1, 1, 2], [2, 1, 1, 1, 1], [2, 1, 3, 1], [3, 1], [10]], [[], [9], [2, 2], [3, 1, 2], [1, 2, 1, 2], [3, 11], [1, 1, 1, 2, 1], [1, 1, 1, 1, 1, 1], [3, 1, 3, 1, 1], [1, 1, 1, 1, 1, 1], [1, 1, 1, 3, 1, 1], [3, 1, 1, 1, 1], [1, 1, 2, 1], [11], []], NN).
nn(13, NN) :- armarNono([[2], [1,1], [1,1], [1,1], [1], [], [2], [1,1], [1,1], [1,1], [1]], [[1], [1,3], [3,1,1], [1,1,3], [3]], NN).
nn(14, NN) :- armarNono([[1,1], [1,1], [1,1], [2]], [[2], [1,1], [1,1], [1,1]], NN).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                              %
%    Predicados auxiliares     %
%        NO MODIFICAR          %
%                              %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%! completar(+S)
%
% Indica que se debe completar el predicado. Siempre falla.
completar(S) :- write("COMPLETAR: "), write(S), nl, fail.

%! mostrarNono(+NN)
%
% Muestra una estructura nono(...) en pantalla
% Las celdas x (pintadas) se muestran como ██.
% Las o (no pintasdas) se muestran como ░░.
% Las no instanciadas se muestran como ¿?.
mostrarNono(nono(M,_)) :- mostrarMatriz(M).

%! mostrarMatriz(+M)
%
% Muestra una matriz. Solo funciona si las celdas
% son valores x, o, o no instanciados.
mostrarMatriz(M) :-
	M = [F|_], length(F, Cols),
	mostrarBorde('╔',Cols,'╗'),
	maplist(mostrarFila, M),
	mostrarBorde('╚',Cols,'╝').

mostrarBorde(I,N,F) :-
	write(I),
	stringRepeat('══', N, S),
	write(S),
	write(F),
	nl.

stringRepeat(_, 0, '').
stringRepeat(Str, N, R) :- N > 0, Nm1 is N - 1, stringRepeat(Str, Nm1, Rm1), string_concat(Str, Rm1, R).

%! mostrarFila(+M)
%
% Muestra una lista (fila o columna). Solo funciona si las celdas
% son valores x, o, o no instanciados.
mostrarFila(Fila) :-
	write('║'),
	maplist(mostrarCelda, Fila),
	write('║'),
	nl.

mostrarCelda(C) :- nonvar(C), C = x, write('██').
mostrarCelda(C) :- nonvar(C), C = o, write('░░').
mostrarCelda(C) :- var(C), write('¿?').
